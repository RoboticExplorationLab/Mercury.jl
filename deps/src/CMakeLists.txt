cmake_minimum_required(VERSION 3.14)
project(Mercury VERSION 0.1 LANGUAGES C)

add_library(hg SHARED
    mercury.h
    mercury.c
)


include(CTest)
enable_testing()

########################################
# Download dependencies
########################################

include(FetchContent)  # pull in the `FetchContent` CMake module

# Download libzmq into `build/_deps
FetchContent_Declare(libzmq
    GIT_REPOSITORY https://github.com/zeromq/libzmq
    GIT_TAG 4097855ddaaa65ed7b5e8cb86d143842a594eebd # version 4.3.4
)

# Include the libzmq CMake files in the current project (adds all of it's targets)
if(NOT libzmq_POPULATED)
    FetchContent_Populate(libzmq)
    set(ZMQ_BUILD_TESTS OFF CACHE BOOL "Test suite for libzmq")
    # To enable building on macOS, see github issue https://github.com/zeromq/libzmq/issues/4085
    set(WITH_TLS OFF CACHE INTERNAL "Disable TLS support")
    add_subdirectory(${libzmq_SOURCE_DIR} ${libzmq_BINARY_DIR})
endif()

find_library(LIBZMQ
    NAMES libzmq
    HINTS "${CMAKE_PREFIX_PATH}/curl/lib"
)

# Modify the `libzmq` target generated by it's build system to include the zmq header
# files as part of its public interface
target_include_directories(libzmq
    PUBLIC
    $<BUILD_INTERFACE:${libzmq_SOURCE_DIR}/include>
)

# Find the libserialport package installed on the computer
find_package(PkgConfig)  # includes the `PkgConfig` CMake module
pkg_check_modules(LIBSP libserialport)  # searches the computer for the libserialport package

# Creates an interface target with the info from the previous command
add_library(libserialport INTERFACE)

# Link the libserial port libraries so that any target linking this also links against the libserialport libraries
target_link_libraries(libserialport
    INTERFACE
    ${LIBSP_LINK_LIBRARIES}
)

# Add the libserialport header directory so that any target linking this also includes the header files
target_include_directories(libserialport
    INTERFACE
    ${LIBSP_INCLUDE_DIRS}
)

# Build the serial_relay library
add_executable(serial_relay
    serial_relay.h
    serial_relay.c
    relay_launch.c
)

# Link against zmq and libserialport (prefer private over public linking)
target_link_libraries(serial_relay
    PRIVATE
    libserialport
    libzmq
)
target_include_directories(serial_relay
    PRIVATE
    $<BUILD_INTERFACE:${libzmq_SOURCE_DIR}/include>
)
